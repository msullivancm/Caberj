#INCLUDE "REPORT.CH"
#INCLUDE "PROTHEUS.CH"
#INCLUDE "TOPCONN.CH"

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณCABR254   บAutor  ณAngelo Henrique     บ Data ณ  22/05/18   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณ Relat๓rio de Totalizadores de PA x Usuแrios.               บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ CABERJ                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/

User Function CABR254()
	
	Local _aArea 	:= GetArea()
	Local oReport	:= Nil
	
	Private _cPerg	:= "CABR254"
	
	//Cria grupo de perguntas
	CABR254A(_cPerg)
	
	If Pergunte(_cPerg,.T.)
		
		//----------------------------------------------------
		//Validando se existe o Excel instalado na mแquina
		//para nใo dar erro e o usuแrio poder visualizar em
		//outros programas como o LibreOffice
		//----------------------------------------------------
		If ApOleClient("MSExcel")
			
			oReport := CABR254B()
			oReport:PrintDialog()
			
		Else
			
			Processa({||CABR254E()},'Processando...')
			
		EndIf
		
	EndIf
	
	RestArea(_aArea)
	
Return


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณCABR254A  บAutor  ณAngelo Henrique     บ Data ณ  22/05/18   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณRotina responsavel pela gera็ใo das perguntas no relat๓rio  บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ CABERJ                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/

Static Function CABR254A(cGrpPerg)
	
	Local aHelpPor := {} //help da pergunta
	
	aHelpPor := {}
	AADD(aHelpPor,"Informe a Data do Protocolo			")	
	
	PutSx1(cGrpPerg,"01","Data De: ? "			,"a","a","MV_CH1"	,"D",TamSX3("ZX_DATDE")[1]	,0,0,"G","",""		,"","","MV_PAR01","","","","","","","","","","","","","","","","",aHelpPor,{},{},"")
	PutSx1(cGrpPerg,"02","Data Ate:? "			,"a","a","MV_CH2"	,"D",TamSX3("ZX_DATDE")[1]	,0,0,"G","",""		,"","","MV_PAR02","","","","","","","","","","","","","","","","",aHelpPor,{},{},"")
	
Return


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณCABR254B  บAutor  ณAngelo Henrique     บ Data ณ  08/05/18   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณRotina que irแ gerar as informa็๕es do relat๓rio            บฑฑ
ฑฑบ          ณ                                                            บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ CABERJ                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/

Static Function CABR254B
	
	Local oReport		:= Nil
	Local oSection1 	:= Nil
	
	oReport := TReport():New("CABR254","TOTALIZADORES DE PA X USUARIOS",_cPerg,{|oReport| CABR254C(oReport)},"TOTALIZADORES DE PA X USUARIOS")
	
	oReport:SetLandScape()
	
	oReport:oPage:setPaperSize(9)
	
	//--------------------------------
	//Primeira linha do relat๓rio
	//--------------------------------
	oSection1 := TRSection():New(oReport,"TOTALIZADORES DE PA X USUARIOS","SZX,SZY")
	
	TRCell():New(oSection1,"ATENDENTE" 			,"SZX")
	oSection1:Cell("ATENDENTE"):SetAutoSize(.F.)
	oSection1:Cell("ATENDENTE"):SetSize(TAMSX3("ZX_USDIGIT")[1])
	
	TRCell():New(oSection1,"PA_MOVIMENTADAS"	,"SZX")
	oSection1:Cell("PA_MOVIMENTADAS"):SetAutoSize(.F.)
	oSection1:Cell("PA_MOVIMENTADAS"):SetSize(10)
	
	TRCell():New(oSection1,"PA_ABERTAS"			,"SZX")
	oSection1:Cell("PA_ABERTAS"):SetAutoSize(.F.)
	oSection1:Cell("PA_ABERTAS"):SetSize(10)
	
Return oReport


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณCABR244B  บAutor  ณAngelo Henrique     บ Data ณ  13/10/17   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณRotina para montar a query e trazer as informa็๕es no       บฑฑ
ฑฑบ          ณrelat๓rio.                                                  บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ CABERJ                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/

Static Function CABR254C(oReport)
	
	Local _aArea 		:= GetArea()
	Local _cQuery		:= ""
	
	Private oSection1 	:= oReport:Section(1)
	Private _cAlias1	:= GetNextAlias()
	
	//---------------------------------------------
	//CABR254D Realiza toda a montagem da query
	//facilitando a manuten็ใo do fonte
	//---------------------------------------------
	_cQuery := CABR254D()
	
	If Select(_cAlias1) > 0
		(_cAlias1)->(DbCloseArea())
	EndIf
	
	DbUseArea(.T.,"TOPCONN",TcGenQry(,,_cQuery),_cAlias1,.T.,.T.)
	
	oSection1:Init()
	oSection1:SetHeaderSection(.T.)
	
	While !(_cAlias1)->(EOF())
		
		oReport:IncMeter()
		
		If oReport:Cancel()
			Exit
		EndIf
		
		oSection1:Cell("ATENDENTE" 		):SetValue( (_cAlias1)->ATEND				)
		oSection1:Cell("PA_MOVIMENTADAS"):SetValue( cValToChar((_cAlias1)->TT_MOV)	)
		oSection1:Cell("PA_ABERTAS"		):SetValue( cValToChar((_cAlias1)->TT_ABT)	)
		
		oSection1:PrintLine()
		
		(_cAlias1)->(DbSkip())
		
	EndDo
	
	oSection1:Finish()
	
	If Select(_cAlias1) > 0
		(_cAlias1)->(DbCloseArea())
	EndIf
	
	RestArea(_aArea)
	
Return(.T.)

/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณCABR254D  บAutor  ณAngelo Henrique     บ Data ณ  24/10/16   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณRotina responsavel por tratar a query, facilitando assim    บฑฑ
ฑฑบ          ณa manuten็ใo do fonte.                                      บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ CABERJ                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/

Static Function CABR254D()
	
	Local _cQuery 	:= ""
	
	_cQuery += "SELECT																																" + CRLF
	_cQuery += "	UPPER(TRIM(ZX_USDIGIT)) ATEND,																									" + CRLF
	_cQuery += "	SUM(TOTAL_MOVIMENTADO) TT_MOV,																									" + CRLF
	_cQuery += "	SUM(TOTAL_DIGIT) TT_ABT																											" + CRLF
	_cQuery += "FROM																																" + CRLF
	_cQuery += "	(																																" + CRLF
	_cQuery += "		SELECT																														" + CRLF
	_cQuery += "			ZX_ABT.ZX_USDIGIT,																										" + CRLF
	_cQuery += "			0 TOTAL_MOVIMENTADO,																									" + CRLF
	_cQuery += "			COUNT(ZX_ABT.ZX_SEQ) TOTAL_DIGIT																						" + CRLF
	_cQuery += "		FROM																														" + CRLF
	_cQuery += "    		" + RetSqlName("SZX") + " ZX_ABT                      																	" + CRLF
	_cQuery += "		WHERE																														" + CRLF
	_cQuery += "			ZX_ABT.ZX_FILIAL = '" + xFilial("SZX") + "'																				" + CRLF
	_cQuery += "			AND ZX_ABT.D_E_L_E_T_ = ' '																								" + CRLF
	_cQuery += "			AND																														" + CRLF
	_cQuery += "				(																													" + CRLF
	_cQuery += "					ZX_ABT.ZX_USDIGIT <> ' '																						" + CRLF
	_cQuery += "					AND																												" + CRLF
	_cQuery += "						UPPER(ZX_ABT.ZX_USDIGIT) NOT IN    (																		" + CRLF
	_cQuery += "																'ADMINISTRADOR',													" + CRLF
	_cQuery += "																'WEB'																" + CRLF
	_cQuery += "															)																		" + CRLF
	_cQuery += "				)																													" + CRLF
	_cQuery += "			AND ZX_ABT.ZX_DATDE BETWEEN '" + DTOS(MV_PAR01) + "' AND '" + DTOS(MV_PAR02) + "'										" + CRLF
	_cQuery += "		GROUP BY ZX_USDIGIT																											" + CRLF
	_cQuery += "																																	" + CRLF
	_cQuery += "		UNION																														" + CRLF
	_cQuery += "																																	" + CRLF
	_cQuery += "			SELECT																													" + CRLF
	_cQuery += "				ZY_USDIGIT,																											" + CRLF
	_cQuery += "				COUNT( DISTINCT SZY.ZY_SEQBA) TOTAL_MOVIMENTADO,																	" + CRLF
	_cQuery += "				0 TOTAL_DIGIT																										" + CRLF
	_cQuery += "			FROM																													" + CRLF
	_cQuery += "    		" + RetSqlName("SZY") + " SZY                      																		" + CRLF
	_cQuery += "			WHERE																													" + CRLF
	_cQuery += "				SZY.ZY_FILIAL = '" + xFilial("SZY") + "'																			" + CRLF
	_cQuery += "				AND SZY.D_E_L_E_T_ = ' '																							" + CRLF
	_cQuery += "				AND SZY.ZY_DTSERV BETWEEN '" + DTOS(MV_PAR01) + "' AND '" + DTOS(MV_PAR02) + "'										" + CRLF
	_cQuery += "				AND SZY.ZY_SEQBA NOT IN (																							" + CRLF
	_cQuery += "											SELECT																					" + CRLF
	_cQuery += "												ZX_ABT.ZX_SEQ PA_ABERTAS															" + CRLF
	_cQuery += "											FROM																					" + CRLF
	_cQuery += "    											" + RetSqlName("SZX") + " ZX_ABT                      								" + CRLF
	_cQuery += "											WHERE																					" + CRLF
	_cQuery += "												ZX_ABT.ZX_FILIAL = '" + xFilial("SZX") + "'											" + CRLF
	_cQuery += "												AND ZX_ABT.D_E_L_E_T_ = ' '															" + CRLF
	_cQuery += "												AND ZX_ABT.ZX_USDIGIT = SZY.ZY_USDIGIT												" + CRLF
	_cQuery += "												AND ZX_ABT.ZX_DATDE BETWEEN '" + DTOS(MV_PAR01) + "' AND '" + DTOS(MV_PAR02) + "'	" + CRLF
	_cQuery += "										)																							" + CRLF
	_cQuery += "				AND																													" + CRLF
	_cQuery += "					(																												" + CRLF
	_cQuery += "						SZY.ZY_USDIGIT <> ' '																						" + CRLF
	_cQuery += "						AND																											" + CRLF
	_cQuery += "						UPPER(SZY.ZY_USDIGIT) NOT IN    (																			" + CRLF
	_cQuery += "															'ADMINISTRADOR',														" + CRLF
	_cQuery += "															'WEB'																	" + CRLF
	_cQuery += "														)																			" + CRLF
	_cQuery += "					)																												" + CRLF
	_cQuery += "			GROUP BY																												" + CRLF
	_cQuery += "				ZY_USDIGIT																											" + CRLF
	_cQuery += "	)																																" + CRLF
	_cQuery += "GROUP BY																															" + CRLF
	_cQuery += "	ZX_USDIGIT																														" + CRLF
	_cQuery += "ORDER BY 1																															" + CRLF 																							
	
Return _cQuery


/*

ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
ฑฑษออออออออออัออออออออออหอออออออัออออออออออออออออออออหออออออัอออออออออออออปฑฑ
ฑฑบPrograma  ณCABR254E  บAutor  ณAngelo Henrique     บ Data ณ  08/05/18   บฑฑ
ฑฑฬออออออออออุออออออออออสอออออออฯออออออออออออออออออออสออออออฯอออออออออออออนฑฑ
ฑฑบDesc.     ณRotina responsavel por gerar o relat๓rio em CSV, pois       บฑฑ
ฑฑบ          ณalguns usuแrios nใo possuem o Excel.                        บฑฑ
ฑฑฬออออออออออุออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออนฑฑ
ฑฑบUso       ณ CABERJ                                                     บฑฑ
ฑฑศออออออออออฯออออออออออออออออออออออออออออออออออออออออออออออออออออออออออออผฑฑ
ฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑฑ
฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿฿
*/

Static Function CABR254E()
	
	Local _aArea 	:= GetArea()
	Local _cQuery	:= ""
	Local nHandle 	:= 0
	Local cNomeArq	:= ""
	Local cMontaTxt	:= ""
	
	Private _cAlias2	:= GetNextAlias()
	
	ProcRegua(RecCount())
	
	cNomeArq := "C:\TEMP\RELATORIO_TOTALIZADOR_PA_X_USUARIOS"
	cNomeArq += "_" + SUBSTR(DTOS(DATE()),7,2)
	cNomeArq += "_" + SUBSTR(DTOS(DATE()),5,2)
	cNomeArq += "_" + SUBSTR(DTOS(DATE()),1,4)
	cNomeArq += "_" + STRTRAN(TIME(),":","_") + ".csv"
	
	//---------------------------------------------
	//CABR244D Realiza toda a montagem da query
	//facilitando a manuten็ใo do fonte
	//---------------------------------------------
	_cQuery := CABR254D()
	
	If Select(_cAlias2) > 0
		(_cAlias2)->(DbCloseArea())
	EndIf
	
	DbUseArea(.T.,"TOPCONN",TcGenQry(,,_cQuery),_cAlias2,.T.,.T.)
	
	While !(_cAlias2)->(EOF())
		
		IncProc()
		
		//------------------------------------------------------------------
		//Se for a primeira vez, deve ser criado o arquivo e o cabe็alho
		//------------------------------------------------------------------
		If nHandle = 0
			
			//------------------------------------------------------------------
			// criar arquivo texto vazio a partir do root path no servidor
			//------------------------------------------------------------------
			nHandle := FCREATE(cNomeArq)
			
			If nHandle > 0
						
				cMontaTxt := "ATENDENTE ; "
				cMontaTxt += "PA_MOVIMENTADAS ; "
				cMontaTxt += "PA_ABERTAS ; "				
				
				cMontaTxt += CRLF // Salto de linha para .csv (excel)
				
				FWrite(nHandle,cMontaTxt)
				
			Else
				
				Aviso("Aten็ใo","Nใo foi possํvel criar o relat๓rio",{"OK"})
				Exit
				
			EndIf
			
		EndIf
	
		cMontaTxt := AllTrim((_cAlias2)->ATEND		) + ";"
		cMontaTxt += cValToChar((_cAlias2)->TT_MOV	) + ";"
		cMontaTxt += cValToChar((_cAlias2)->TT_ABT	) + ";"
				
		cMontaTxt += CRLF // Salto de linha para .csv (excel)
		
		FWrite(nHandle,cMontaTxt)
		
		(_cAlias2)->(DbSkip())
		
	EndDo
	
	If Select(_cAlias2) > 0
		(_cAlias2)->(DbCloseArea())
	EndIf
	
	If nHandle > 0
		
		// encerra grava็ใo no arquivo
		FClose(nHandle)
		
		MsgAlert("Relatorio salvo em: "+cNomeArq)
		
	EndIf
	
	RestArea(_aArea)
	
Return